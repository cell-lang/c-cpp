type Text = String, Text*;


String remove_path(String str) {
  idx = length(str) - 1;
  rev_chs = ();
  while idx >= 0 {
    ch = str[idx];
    if ch == `.` {
      idx = idx - 1;
      while idx >= 0 {
        ch = str[idx];
        break if ch != `.` and not is_digit(ch) and not is_lower(ch) and not is_upper(ch);
        idx = idx - 1;
      }
    }
    else {
      rev_chs = (rev_chs | ch);
      idx = idx - 1;
    }
  }
  return string(reverse(rev_chs));
}


String* flatten(Text text) {
  return if is_str(text) then (text) else flatten(text, 0, ());

  String* flatten(Text* text, Nat indent_level, String* lines) {
    has_strings = (t <- text : is_str(t) and t != "" and (length(t) < 2 or take(t, 2) != "//"));
    next_indent_level = nat(indent_level + if has_strings then 1 else 0);
    new_lines = lines;
    for t <- text {
      if is_str(t)
        new_lines = (new_lines | indent_line(t, indent_level));
      else
        new_lines = flatten(t, next_indent_level, new_lines);
    }
    return new_lines;
  }

  String indent_line(String str, Nat indent_level) =
    if str != ""
      then string((` ` : _ < 2 * indent_level)) & str
      else "";

  True  is_str(String) = true;
  False is_str(Text*)  = false;
}


[LambdaExpr] lambda_exprs(CompiledPrg prg) {
  lambda_exprs = select_rec(prg, $ :: LambdaExpr);
  fail if not lambda_exprs :: [LambdaExpr];
  return lambda_exprs;
}


type AuxVar = curr_time, PrevVarValue;


type VarTypesNotToDeclare = FnArg,
                            ImplArg,
                            CaptVar,
                            ElemVar,
                            AnyMembVar,
                            PendingStateVar,
                            AnySignalVar,
                            PendingInputVar,
                            AnyAutoVar,
                            MembBoolVar,
                            QualBoolVar,
                            MembIntVar,
                            update_expr_index,
                            MembObjVar,
                            LocAutoIntVar,
                            ProjdIntVar;


## BAD: THIS THING IS INCREDIBLY MESSY. I MUST IMPLEMENT A VERSION OF SELECT WITH A <STOP> GUARD
[AnyVar] vars_to_declare(Instr* instrs) {
  vars = select(
    instrs,
    $ :: <AnyVar, ClsDef> and not $ :: <VarTypesNotToDeclare, ClsDef>
  );
  fail if not vars :: [AnyVar];
  return vars - [:invalid_var]; ## THIS IS A REALLY, REALLY UGLY THING
}

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

String parenthesize(String code, Bool parenthesized) = if parenthesized then "(" & code & ")" else code;


String uppercase_encoding(Nat n) {
  m = n;
  chs = ();
  while m > 0 or chs == () {
    chs = (nat(ascii_upper_a + mod(n, 26))) & chs;
    m = m / 26;
  }
  return string(chs);
}

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

[Any] select(Any obj, (Any -> Bool) cond) =
  if cond(obj)
    then [obj]
    else match (obj)
      <+>         = [],
      <*..*>      = [],
      <!>         = [],
      ()          = union([select(x, cond) : x <- obj]),
      []          = union([select(x, cond) : x <- obj]),
      [,]         = union([select(l, cond) & select(r, cond) : l r <- obj]),
      [,,]        = union([select(l, cond) & select(m, cond) & select(r, cond)  : l m r <- obj]),
      tag?(iobj?) = select(iobj, cond); ## SHOULD I EXTEND THE SEARCH TO THE TAG AS WELL?


[Any] select_rec(Any obj, (Any -> Bool) cond) =
  [obj if cond(obj)] &
  match (obj)
      <+>         = [],
      <*..*>      = [],
      <!>         = [],
      ()          = union([select_rec(x, cond) : x <- obj]),
      []          = union([select_rec(x, cond) : x <- obj]),
      [,]         = union([select_rec(l, cond) & select_rec(r, cond) : l r <- obj]),
      [,,]        = union([select_rec(l, cond) & select_rec(m, cond) & select_rec(r, cond) : l m r <- obj]),
      tag?(iobj?) = select_rec(iobj, cond); ## SHOULD I EXTEND THE SEARCH TO THE TAG AS WELL?


// Any replace(Any obj, (Any -> Bool) cond, (Any -> Any) eval) =
//   if cond(obj)
//     then eval(obj)
//     else match (obj)
//       +           = obj,
//       *           = obj, ## BAD
//       ^           = obj, ## BAD
//       {...}       = {replace(x, cond, eval) : x <- obj},
//       [...]       = [replace(x, cond, eval) : x <- obj],
//       (...)       = (replace(k, cond, eval) => replace(v, cond, eval) : v @ k <- obj),
//       tag @ iobj  = tag @ replace(iobj, cond, eval); ## SHOULD I EXTEND THE REPLACEMENT TO THE TAG AS WELL?

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

implicit prg : CompiledPrg {
  SymbObj+ program_symbols {
    fixed_symbs = (object(a) : a <- special_symbols);
    other_symbs = select(prg, $ :: SymbObj) - set(fixed_symbs);
    fail if not other_symbs :: [SymbObj];
    // other_symbs = isort(other_symbs);
    other_symbs = sort_set(other_symbs, lower_than(bare_obj($a), bare_obj($b)));
    return nonempty(fixed_symbs & other_symbs);


    Atom+ special_symbols = (false, true, :void, :string, :date, :time, :nothing, :just, :success, :failure);


    Bool lower_than(Atom a1, Atom a2) {
      assert not in(a1, special_symbols) and not in(a2, special_symbols);

      chs1 = chars(_print_(a1));
      chs2 = chars(_print_(a2));

      len1 = |chs1|;
      len2 = |chs2|;

      return len1 < len2 if len1 != len2;

      for ch1 @ i <- chs1 {
        ch2 = chs2(i);
        return ch1 < ch2 if ch1 != ch2;
      }

      fail;
    }
  }


  LambdaExpr* sorted_lambda_exprs = isort(lambda_exprs(prg));


  (String, Nat)* int_seqs_types_and_lengths = ((native_int_type(min(s), max(s)), |s|) : s <- prg.int_seqs);


  [ImplArg -> [+FnId]] cached_semi_consts_by_impl_arg {
    semi_consts = [p : p <- prg.procs, p.cached and p.impl_args != []];
    impl_args_by_fn = [cast_to_fn_symbol(p.name) -> p.impl_args : p <- semi_consts];
    return reverse_multi(impl_args_by_fn);

    FnId cast_to_fn_symbol(ObjFnName id) = if id :: FnId then id else undefined;
  }


  [[+Symbol] -> (Symbol+, Nat)] symbols_sets_info {
    mk_rec_exprs = select(prg, match ($) mk_record() = true, _ = false);
    fail if not mk_rec_exprs :: [<mk_record(fields: [+SymbObj -> AtomicExpr])>];
    symbs_sets = [[untag(l) : l _ <- e.fields] : e <- mk_rec_exprs] &
                 [keys(untag(r)) : _ rs <- prg.opt_reprs, r <- rs];
    ord_symbs_sets = isort(symbs_sets);

    symbols_map = [untag(s) -> i : s @ i <- program_symbols];
    return [ls -> {
        ord_labels = sort_set(ls, symbols_map($a, !) < symbols_map($b, !));
        return (nonempty(ord_labels), i);
      } : ls @ i <- ord_symbs_sets
    ];
  }
}
