implicit prg : CompiledPrg, typedefs : [TypeName -> NeType], generated : [BasicTypeSymbol] {
  String* gen_inter_types_decls {
    type_decls, subtype_decls = inter_types_decls;

    native_decls = ();
    for intertypes <- topological_sort(group_by_left_arg(subtype_decls))
      for intertype <- isort(intertypes)
        if not type_decls(intertype, _) {
          parents = isort([gen_class_name(p) : p <- subtype_decls(intertype, ?)]);
          inheritance_list = if parents != () then " : " & append(("public " & p : p <- parents), ", ") else "";
          native_decls = (native_decls | "") if native_decls != ();
          class_name = gen_class_name(intertype);
          native_decls = (native_decls |
            "class " & class_name & inheritance_list & " {",
            "  public:",
            "    virtual ~"  & class_name & "() {}",
            "};"
          );
        }

    for ts tdef <- type_decls {
      parents = isort([gen_class_name(p) : p <- subtype_decls(ts, ?)]);
      native_decl = native_inter_type_decl(tdef, gen_class_name(ts), parents);
      native_decls = native_decls & native_decl;
    }

    return native_decls;
  }


  //////////////////////////////////////////////////////////////////////////////
  //////////////////////////////////////////////////////////////////////////////

  String* native_inter_type_decl(DeclInterType inter_type, String class_name, String* parents) {
    record_type = match (inter_type) tagged(_, t?) = t;
    fields = untag(record_type);
    labels = [s : s _ <- fields];
    fields_names = adjusted_unambiguous_snake_case_names(labels);
    labels = sort_record_labels(labels);
    fields_types = (return_value_type(fields(l)) : l <- labels);
    max_len = max([length(t) : t <- fields_types]);
    inheritance_list = if parents != () then " : " & append(("public " & p : p <- parents), ", ") else "";
    lines = ("", "class " & class_name & inheritance_list &  " {", "  public:");
    for l @ i <- labels {
      name = fields_names(l);
      type = fields_types(i);
      spaces = string((` ` : _ < max_len - length(type) + 1));
      line = "    " & type & spaces & name & ";";
      lines = (lines | line);
    }
    lines = (lines | "};");
    return lines;
  }
}
